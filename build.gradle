plugins {
    id "com.modrinth.minotaur" version "2.+"
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

def getLatestGitTag() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch(Exception e) {
        e.printStackTrace()
        return "unknown"
    }
}

def tag = System.getenv("TAG") ?: getLatestGitTag()
def isSnapshot = System.getenv('SNAPSHOT')?.toBoolean() ?: false
def gitHash = System.getenv("GIT_HASH") ?: "local"

version = isSnapshot ? "$tag-SNAPSHOT-${gitHash.take(7)}" : (System.getenv("TAG") ? tag : "dev-$tag")
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "noctisui" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Lombok
    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {

    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

def fetchReleaseDescription = { String releaseTag ->
    try {
        if (!releaseTag || releaseTag == 'unknown' || releaseTag.startsWith('dev-')) {
            logger.lifecycle("modrinth: release tag is '${releaseTag}', falling back to README.md")
            return file("README.md").text
        }

        def repo = 'AxenoDev/NoctisUI'
        def apiUrl = "https://api.github.com/repos/${repo}/releases/tags/${releaseTag}"
        def connection = new URL(apiUrl).openConnection()
        def ghToken = System.getenv('GITHUB_TOKEN')
        if (ghToken) {
            connection.setRequestProperty('Authorization', "token ${ghToken}")
        }
        connection.setRequestProperty('Accept', 'application/vnd.github.v3+json')
        connection.connect()

        if (connection.responseCode == 200) {
            def text = connection.inputStream.text
            def json = new groovy.json.JsonSlurper().parseText(text)
            if (json.body) {
                logger.lifecycle("modrinth: using GitHub release description for tag ${releaseTag}")
                return json.body.toString()
            }
        } else {
            logger.lifecycle("modrinth: no GitHub release found for tag ${releaseTag} (HTTP ${connection.responseCode}), falling back to README.md")
        }
    } catch (Exception e) {
        logger.lifecycle("modrinth: failed to fetch release description (${e.message}), falling back to README.md")
    }
    return file("README.md").text
}

def writeModrinthBodyFile = { String text ->
    def outFile = file("${buildDir}/modrinth-description.md")
    outFile.parentFile.mkdirs()
    outFile.text = text
    return outFile
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "noctisui"
    versionNumber = version
    versionName = "NoctisUI ${version}"
    uploadFile = remapJar
    gameVersions = ["${minecraft_version}"]
    loaders = ["fabric"]

    def modrinthBody = fetchReleaseDescription(tag)
    syncBodyFrom = writeModrinthBodyFile(modrinthBody).absolutePath
}

tasks.register('printModrinthBody') {
    group = 'help'
    description = 'Print the Modrinth description body chosen for this version (fetches GitHub release body or falls back to README.md) and show the file path'
    doLast {
        def body = fetchReleaseDescription(tag)
        def outFile = writeModrinthBodyFile(body)
        println "Modrinth description written to: ${outFile.absolutePath}"
        println '---- Modrinth description start ----'
        println body
        println '---- Modrinth description end ----'
    }
}

publishing {
    publications {
        create("maven", MavenPublication) {
            from components.java
            artifactId = 'noctisui'

            pom {
                name = 'NoctisUI'
                description = 'NoctisUI is a Minecraft UI library that makes it easy to create menus and HUDs.'
                url = 'https://github.com/AxenoDev/NoctisUI'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://opensource.org/licenses/GPL-3.0'
                    }
                }

                developers {
                    developer {
                        id = 'AxenoDev'
                        name = 'AxenoDev'
                        url = 'https://axeno.me'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/AxenoDev/NoctisUI.git'
                    developerConnection = 'scm:git:ssh://github.com:AxenoDev/NoctisUI.git'
                    url = 'https://github.com/AxenoDev/NoctisUI'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/AxenoDev/NoctisUI")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.jar {
    manifest {
        attributes 'GIT-COMMIT': gitHash
    }
}

tasks.test {
    useJUnitPlatform()
}

tasks.named('jar') {
    from(sourceSets.main.output)
    exclude('**/test/**')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}